/* ************ GREEN ARABIA ************ */
/* 04 APR 01 */

/*
 * "Green Arabia"
 * by _KickMyButt
 *
 * check out this webpage for more information:
 * http://www.ageofkings.cc/readPosting.asp?PostingId=340301
 *
 */
#const BURMESE_REVEALER  		1378
#include_drs random_map.def 54000

/* ****************************************************** */
<PLAYER_SETUP>

random_placement

/* ****************************************************** */
<LAND_GENERATION>

base_terrain GRASS

create_player_lands
{
	terrain_type					GRASS
	land_percent					25
	base_size						9
	other_zone_avoidance_distance	7
	border_fuzziness				15
	clumping_factor					15
	set_zone_by_team
}

/* ****************************************************** */
<TERRAIN_GENERATION>

/* PRIMARY FOREST */
create_terrain FOREST
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	5
	land_percent					14
	number_of_clumps				13
	clumping_factor					100
	set_avoid_player_start_areas
	set_scale_by_groups
}

/* SECONDARY FOREST */
create_terrain PINE_FOREST
{
	base_terrain					FOREST
	spacing_to_other_terrain_types	0
	land_percent					20
	number_of_clumps				5
	clumping_factor					100
	set_avoid_player_start_areas
	set_scale_by_groups
}

/* PATCH #1 */
create_terrain GRASS3
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					3
	number_of_clumps				40
	clumping_factor					1
	set_avoid_player_start_areas
	set_scale_by_size
}

/* PATCH #2 */
create_terrain GRASS2
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					7
	number_of_clumps				40
	set_scale_by_size
}

/* PATCH #3 */
create_terrain DIRT3
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	0
	land_percent					1
	number_of_clumps				15
	set_scale_by_size
}

/* PATCH #4 */
create_terrain DIRT
{
	base_terrain					GRASS
	spacing_to_other_terrain_types	3
	land_percent					1
	if TINY_MAP
		number_of_clumps			10
	else
		number_of_clumps			20
	endif
	clumping_factor					1
	set_avoid_player_start_areas
	set_scale_by_size
}

/* PATCH #4+ */
create_terrain DIRT3
{
	base_terrain					DIRT
	spacing_to_other_terrain_types	0
	land_percent					0
	if TINY_MAP
		number_of_clumps			100
	else
		number_of_clumps			200
	endif
	clumping_factor					1
	set_scale_by_size
}

create_terrain ROAD2
{
	number_of_tiles 1
	set_avoid_player_start_areas
	number_of_clumps 5
}

/* ****************************************************** */
<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
	set_place_for_every_player
	group_placement_radius			18
	min_distance_to_players			0
	max_distance_to_players			0
}

create_object VILLAGER
{
	set_place_for_every_player
	min_distance_to_players			6
	max_distance_to_players			6
}

create_object SCOUT
{
	number_of_objects				1
	set_place_for_every_player
	min_distance_to_players			7
	max_distance_to_players			9
}

create_object RELIC 
{
	number_of_objects 5
	terrain_to_place_on ROAD2
	min_distance_to_players 25
	set_gaia_object_only 
}

create_object BURMESE_REVEALER
{
	number_of_objects 10
	terrain_to_place_on ROAD2
	min_distance_to_players 25 
	set_place_for_every_player
}


/* SPECIAL STUFF FOR REGICIDE */
if REGICIDE
create_object KING
{
	set_place_for_every_player
	min_distance_to_players			6
	max_distance_to_players			6
}
endif

/* RESOURCES BY PLAYER */

/* BERRIES */
create_object FORAGE
{
	number_of_objects				6
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			10
	max_distance_to_players			12
	min_distance_group_placement	6
}

/* BOARS */
create_object BOAR
{
	number_of_objects				1
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			16
	max_distance_to_players			22
}

create_object BOAR
{
	number_of_objects				1
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			16
	max_distance_to_players			22
}

/* BIG STONE */
create_object STONE
{
	number_of_objects				5
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			14
	max_distance_to_players			18
	min_distance_group_placement	7
}

/* BIG GOLD */
create_object GOLD
{
	number_of_objects				7
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			12
	max_distance_to_players			16
	min_distance_group_placement	7
}

/* STONE */
create_object STONE
{
	number_of_objects				4
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			20
	max_distance_to_players			26
	min_distance_group_placement	14
}

/* GOLD */
create_object GOLD
{
	number_of_objects				4
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			18
	max_distance_to_players			26
	min_distance_group_placement	7
}

create_object GOLD
{
	number_of_objects				4
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			25
	max_distance_to_players			35
	min_distance_group_placement	7
}

/* BAA BAA */
create_object SHEEP
{
	number_of_objects				4
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			10
	max_distance_to_players			12
}

create_object SHEEP
{
	number_of_groups				2
	number_of_objects				2
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			14
	max_distance_to_players			30
}

/* DEER */
create_object DEER
{
	number_of_objects				4
	group_placement_radius			3
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			18
	max_distance_to_players			24
	min_distance_group_placement	9
}

create_object DEER
{
	number_of_objects				4
	group_variance					1
	set_loose_grouping
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			40
	min_distance_group_placement	9
}

/* STRAGGLER TREES */
create_object OAKTREE
{
	number_of_objects				2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			4
	max_distance_to_players			5
	min_distance_group_placement	2
}
create_object PINETREE
{
	number_of_objects				3
	set_gaia_object_only
	set_place_for_every_player
	min_distance_to_players			6
	max_distance_to_players			8
	min_distance_group_placement	2
}

/* RESOURCES BY MAP */

/* STRAGGLER TREES */
create_object BAMBOO_TREE
{
	number_of_objects				12
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players			8
	min_distance_group_placement	2
	terrain_to_place_on				GRASS2
}

create_object PINETREE
{
	number_of_objects				18
	set_gaia_object_only
	set_scaling_to_map_size
	min_distance_to_players			8
}

/* EXTRA BERRIES */
create_object FORAGE
{
	if TINY_MAP
		number_of_groups			0
	elseif SMALL_MAP
		number_of_groups			0
	elseif MEDIUM_MAP
		number_of_groups			0
	elseif LARGE_MAP
		number_of_groups			1
	elseif HUGE_MAP
		number_of_groups			2
	elseif GIGANTIC_MAP
		number_of_groups			3
	endif
	number_of_objects				5
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			40
	max_distance_to_players			120
	min_distance_group_placement	9
}

/* EXTRA GOLD */
create_object GOLD
{
	if TINY_MAP
		number_of_groups		2
		number_of_objects		3
	elseif SMALL_MAP
		number_of_groups		2
		number_of_objects		3
	elseif MEDIUM_MAP
		number_of_groups		3
		number_of_objects		3
		group_variance			1
	elseif LARGE_MAP
		number_of_groups		3
		number_of_objects		3
		group_variance			1
	elseif HUGE_MAP
		number_of_groups		4
		number_of_objects		4
		group_variance			1
	elseif GIGANTIC_MAP
		number_of_groups		5
		number_of_objects		4
		group_variance			1
	endif	
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			40
	min_distance_group_placement	24
}

/* EXTRA STONE */
create_object STONE
{
	if TINY_MAP
		number_of_groups		1
	elseif SMALL_MAP
		number_of_groups		1
	elseif MEDIUM_MAP
		number_of_groups		2
	elseif LARGE_MAP
		number_of_groups		3
	elseif HUGE_MAP
		number_of_groups		4
	elseif GIGANTIC_MAP
		number_of_groups		5
	endif
	number_of_objects			4
	group_variance				1
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			40
	min_distance_group_placement	24
}

/* WOLVES */
create_object WOLF
{
	number_of_groups				3
	number_of_objects				1
	set_scaling_to_map_size
	set_gaia_object_only
	min_distance_group_placement	12
	min_distance_to_players			40
	max_distance_to_players			120
}

create_object WOLF
{
	number_of_objects				2
	set_gaia_object_only
	set_place_for_every_player
	min_distance_group_placement	12
	min_distance_to_players			30
	max_distance_to_players			40
}

/* BIRDIES */
create_object HAWK
{
	number_of_objects				6
	set_scaling_to_map_size
}

/* ****************************************************** */
<ELEVATION_GENERATION>

if TINY_MAP
	create_elevation				4
	{
		base_terrain				GRASS
		number_of_clumps			10
		number_of_tiles				2000
		set_scale_by_groups
		set_scale_by_size
	}
else
	create_elevation				7
	{
		base_terrain				GRASS
		number_of_clumps			15
		number_of_tiles				3000
		set_scale_by_groups
		set_scale_by_size
	}
endif

/* ****************************************************** */
<CLIFF_GENERATION>

if TINY_MAP
	min_number_of_cliffs			4
	max_number_of_cliffs			6
	min_length_of_cliff				3
	max_length_of_cliff				6
	cliff_curliness					0
	min_distance_cliffs				3
else
	min_number_of_cliffs			5
	max_number_of_cliffs			8
	min_length_of_cliff				4
	max_length_of_cliff				10
	cliff_curliness					5
	min_distance_cliffs				3
endif
